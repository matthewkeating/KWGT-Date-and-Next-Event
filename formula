$
/* version: 1.0.0 */

/* if no events today */
if ((ci(ecount, a0d)+ci(acount, a0d)<1),
	"No events today",

/* if the next-next event <= 10 mintues away (this handles the case of notifying when two events are back-to-back) */
if ((df(D,ci(start,e0))=df(D) &	/* the next event must be today */
	 df(D,ci(start,e1))=df(D) & /* the next-next event must also be today */
	 ((df(H,ci(start, e1))*60)+(df(mm,ci(start,e1))))-((df(H)*60)+df(mm))<=10),

	tc(ell, ci(title, e1), 15) + " in " + (tf(ci(start, e1), m)+1) + " mins",

/* if the next event is <=60 mintues away (but has not started) */
if ((((df(H,ci(start, e0))*60)+(df(mm,ci(start,e0))))-((df(H)*60)+df(mm))>0 &	/* ensures the event has not started */
	 ((df(H,ci(start, e0))*60)+(df(mm,ci(start,e0))))-((df(H)*60)+df(mm))<=60), 
	
	tc(ell, ci(title, e0), 15) + " in " + (tf(ci(start, e0), m)+1) + " mins",

/* e0 is underway */
if (((tf(ci(start, e0), m) <= 0) &	/* event has started */
	 (tf(ci(end, e0), m) > 0)),	/* event has not ended */
	
	tc(ell, ci(title, e0), 20) + " now",

/* ASSERT: the next event is > 60 minutes away */

/* if there is exactly one all-day event */
if((ci(acount,a0d)=1),
	if((ci(cname, a0, a0d)="Birthdays"),
		ci(title, a0) + "'s Birthday",	
	ci(title, a0)),

/* if there is more than one all-day event */
if((ci(acount,a0d)>1),
	ci(acount,a0d) + " all-day events",

/* ASSERT: the next event is not today, so everything below only has to deal with all day events */

/* if there are no all-day events */
if((ci(acount,a0d)<=0),
	"No more events today",
	"Unexpected: No conditions met" /* when none of the above conditions are met */

/* programming note: there will be exactly the same number of ) below as there are fully left-aligned if statements above */
)))))))
$
